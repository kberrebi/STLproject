----------------------------------------------- -*- text -*-
-- Langage MiniJava : Sous-ensemble du langage Java pour le projet STL 2016-2017
-- Sémantique : Construction de l'arbre abstrait
--                      Gestion de la table des symboles
--                      Typage
--                      Calcul des déplacements (Allocation mémoire)
--                      Génération de code TAM
-----------------------------------------------
option auto= true;
option version = 0.0.1 ;
option k=2;



inh tds		: SymbolTable for Interfaces, Interface, HeritageInterface, SuiteHeritageInterface, ElementsInterface, ElementInterface, Signature, Type, TypeAtomique , SuiteTypeAtomique , Affectation, SuiteAffectation, Expression, Expressions, SuiteExpressions, Condition, SuiteCondition, Terme, SuiteTerme, Facteur, SuiteFacteur, Valeur, SuiteNouveau, Arguments, Acces, AppelOuAcces, Parametre, SuiteParametres, Parametres, Classe, HeritageClasse, ImplantationInterface, ElementsClasse, Classes, AttributOuMethode, SuiteAttributOuMethode, ElementClasse, Constructeur, Principale, MethodePrincipale, Instruction, Instructions, SuiteConditionnelle , Bloc;

inh tds_Params 	: SymbolTable for Parametre, Parametres, SuiteParametres;

inh tdsMeth     : SymbolTable for Classe, ElementClasse, AttributOuMethode, SuiteAttributOuMethode, ElementsClasse;

inh tdsAtt      : SymbolTable for Classe, ElementClasse, ElementsClasse, AttributOuMethode,SuiteAttributOuMethode;

inh factory	: BlockFactory for Interfaces, Interface, HeritageInterface, SuiteHeritageInterface, ElementsInterface, ElementInterface, Signature, Type, TypeAtomique , SuiteTypeAtomique , Affectation, SuiteAffectation, Expression, Expressions, SuiteExpressions, Condition, SuiteCondition, Terme, SuiteTerme, Facteur, SuiteFacteur, Valeur, SuiteNouveau, Arguments,Acces, AppelOuAcces, Parametre, SuiteParametres, Parametres, Classe, HeritageClasse, ImplantationInterface, ElementsClasse, Classes, DroitAcces, AttributOuMethode, SuiteAttributOuMethode, ElementClasse, Constructeur, Principale, MethodePrincipale, Instruction, Instructions, SuiteConditionnelle, Bloc;

inh listHeritage: List<String> for SuiteHeritageInterface;
inh type	: Type for SuiteTypeAtomique ;

---------------------------------------------------------------------------------------------------------------
syn ast		: Programme for Programme ; 
syn ast		: LinkedList<Interface> for Interfaces, HeritageInterface, SuiteHeritageInterface;
syn ast		: Interface for Interface;
syn ast		: LinkedList<ElementInterface> for ElementsInterface;
syn ast 	: ElementInterface for ElementInterface;
syn ast 	: Signature for Signature;
syn ast 	: Type for Type, TypeAtomique , SuiteTypeAtomique ;
syn bin_op	: BinaryOperator for Additif, Multiplicatif, Comparatif;

syn ast 	: Expression for Facteur, SuiteFacteur, SuiteNouveau, AppelOuAcces, Acces, Terme, SuiteTerme, Condition, SuiteCondition, Affectation, SuiteAffectation, Expression, Valeur;

syn ast		: Expressions for Expressions, SuiteExpressions;
syn ast 	: Arguments for Arguments;

syn ast         : Parametre for Parametre;
syn ast         : LinkedList<Parametre> for Parametres, SuiteParametres;

inh gauche 	: Expression for SuiteAffectation, SuiteCondition, SuiteFacteur, SuiteTerme;

inh type 	: Type for SuiteNouveau;
inh ident 	: String for AppelOuAcces, Acces;

syn ast 	: Classe for Classe;
syn ast 	: LinkedList<Classe> for HeritageClasse;
syn ast 	: LinkedList<Interface> for ImplantationInterface;
syn ast 	: LinkedList<ElementClasse> for ElementsClasse;
syn ast 	: LinkedList<Classe> for Classes;

inh droit 	: DroitAcces for ElementClasse, AttributOuMethode, SuiteAttributOuMethode; 
inh nomAtt 	: String for SuiteAttributOuMethode;
inh nomClasse : String for ElementsClasse,ElementClasse, Constructeur;
inh typeAtt : Type for SuiteAttributOuMethode;
syn ast 	: DroitAcces for DroitAcces;
syn ast 	: ElementClasse for ElementClasse, AttributOuMethode, SuiteAttributOuMethode, Constructeur; 
syn ast 	: Block for Bloc;


syn ast : MethodePrincipale for Principale, MethodePrincipale;

inh tree : List<Instruction> for Instructions;

syn insts : List<Instruction> for Instructions;

syn ast : Instruction for Instruction;

syn ast : Block for SuiteConditionnelle;





space  separateur				is    "[\n\r\t ]+";
space  commentaire			    is    "\/\/.*\n";
sugar  accolade_ouvrante		is    "\{";  
sugar  accolade_fermante		is    "\}";
sugar  crochet_ouvrant			is    "\[";  
sugar  crochet_fermant			is    "\]";  
sugar  parenthese_ouvrante		is    "\(";
sugar  parenthese_fermante		is    "\)";
sugar  inferieur				is    "\<";  
sugar  superieur				is    "\>";  
sugar  inferieur_egal			is    "\<=";  
sugar  superieur_egal			is    "\>=";  
sugar  point					is    "\.";
sugar  point_virgule			is    ";";  
sugar  virgule					is    ",";
sugar  affectation				is    "=";
sugar  egalite					is    "==";  
sugar  different				is    "\!=";  
sugar  addition				    is    "\+";
sugar  soustraction				is    "\-";
sugar  ou						is    "\|\|";  
sugar  multiplication			is    "\*";
sugar  division				    is    "/";
sugar  modulo					is    "%";
sugar  adresse					is    "&";  
sugar  negation				    is    "\!";  
sugar  et						is    "&&";  
sugar  vrai				    	is    "true";
sugar  faux					    is    "false";
sugar  si						is    "if";
sugar  sinon					is    "else";
sugar  afficher					is    "print";  
sugar  nouveau				    is    "new";  
sugar  tant_que				    is    "while";  
sugar  retour					is    "return";  
sugar  type_int				    is    "int";
sugar  type_bool				is    "boolean";  
sugar  type_char				is    "char";        
sugar  type_String				is    "String";        
sugar  vide					    is    "void";     
sugar  nul						is    "null";     
sugar  choix					is    "\?";         
sugar  deux_points				is    ":";          
sugar  interface				is    "interface";     
sugar  classe					is    "class";      
sugar  extension				is    "extends";      
sugar  implantation				is    "implements";      
sugar  public					is    "public";    
sugar  protege					is    "protected";
sugar  prive					is    "private";
sugar  statique					is    "static";
sugar  final				    is    "final";
sugar  principale				is    "main";
term   chaine					is    "\"([^\"]|\\\")*\"";
term   caractere				is    "\'[^\']\'";
term   entier					is    "[0-9]+";
term   identificateur			is    "[a-z_][a-zA-Z0-9_]*";
term   identificateur_type		is    "[A-Z][a-zA-Z0-9_]*"; 



Programme -> #inter Interfaces #class Classes #principal Principale #ast;
global
    s : SymbolTable;
    t : SymbolTable;
#inter {
do
    s := new SymbolTable();
    Interfaces^tds := s;
    Interfaces^factory := new BlockFactoryImpl();
end
}
#class {
do
    t := new SymbolTable(s);
    Classes^tds := t;
    Classes^factory := new BlockFactoryImpl();
end
}
#principal {
do
    Principale^tds := new SymbolTable(t);
    Principale^factory := new BlockFactoryImpl();
end
}

#ast {
do
    Programme^ast := new ProgrammeImpl(Interfaces^ast, Classes^ast, Principale^ast);
end
}





Interfaces ->  Interface #tds  Interfaces #ast;
#tds{
do
	Interfaces^tds.register(Interface^ast);
	Interfaces1^tds := Interfaces^tds;
end
}

#ast{
do
	Interfaces1^ast.addFirst(Interface^ast);
	Interfaces^ast := Interfaces1^ast;
end
}



Interfaces -> #ast;
#ast{
do
	Interfaces^ast := new LinkedList<Interface>();
end
}

Interface -> interface identificateur_type HeritageInterface accolade_ouvrante #tds ElementsInterface accolade_fermante #ast;
#tds{
do
		ElementsInterface^tds := new SymbolTable(Interface^tds); 
end
}

#ast{
do
	Interface^ast := Interface^factory.createInterface(identificateur_type^txt, HeritageInterface^ast, ElementsInterface^ast);
end
}


HeritageInterface -> extension identificateur_type #tds SuiteHeritageInterface #ast;
#tds{
local
   l : List<String>;
do
	if (HeritageInterface^tds.knows(identificateur_type^txt)) then
		l := new LinkedList<String>();
		l.add(identificateur_type^txt);
		SuiteHeritageInterface^listHeritage := l;
	else
		error(Interface_undefined_ident, identificateur_type^txt);
	end	
end
}


#ast{
local
   f : Optional<Declaration>;
   d : Declaration;
do
	f := HeritageInterface^tds.get(identificateur_type^txt);
	d := f.get();
	match d
      		with Interface then SuiteHeritageInterface^ast.addFirst(d);
      	end
	--SuiteHeritageInterface^ast.addFirst(d);
	HeritageInterface^ast := SuiteHeritageInterface^ast;

end
}


HeritageInterface -> #ast;
#ast{
do
	HeritageInterface^ast := new LinkedList<Interface>();
end
}

SuiteHeritageInterface -> virgule identificateur_type #tds SuiteHeritageInterface #ast;
#tds{
do
	if (SuiteHeritageInterface^tds.knows(identificateur_type^txt)) then
		SuiteHeritageInterface^listHeritage.add(identificateur_type^txt);
		SuiteHeritageInterface1^listHeritage := SuiteHeritageInterface^listHeritage;
	else
		error(Interface_undefined_ident, identificateur_type^txt);
	end	
end
}


#ast{
local
   f : Optional<Declaration>;
   d : Declaration;
do
	f := SuiteHeritageInterface^tds.get(identificateur_type^txt);
	d := f.get();
	match d
      		with Interface then SuiteHeritageInterface1^ast.addFirst(d);
      	end
	--SuiteHeritageInterface1^ast.addFirst(d);
	SuiteHeritageInterface^ast := SuiteHeritageInterface1^ast;

end
}


SuiteHeritageInterface -> #ast;
#ast{
do
	SuiteHeritageInterface^ast := new LinkedList<Interface>();
end
}


ElementsInterface -> ElementInterface #tds ElementsInterface #ast;
#tds{
do
	ElementsInterface^tds.register(ElementInterface^ast);
	ElementsInterface1^tds := ElementsInterface^tds;
end
}

#ast{
do
	ElementsInterface1^ast.addFirst(ElementInterface^ast);
	ElementsInterface^ast := ElementsInterface1^ast;
end
}

ElementsInterface ->  #ast;
#ast{
do
	ElementsInterface^ast := new LinkedList<ElementInterface>();
end
}

ElementInterface -> final statique Type identificateur affectation Expression point_virgule #ast;
#ast{
do
	if (ElementInterface^tds.knows(identificateur^txt)) then
		error(field_ident_already_defined, identificateur^txt);
	else
		ElementInterface^ast := ElementInterface^factory.createStaticField(Type^ast, identificateur^txt, Expression^ast);
		ElementInterface^ast.setFinal(true);
	end
end
}

ElementInterface ->  Signature point_virgule #ast;
#ast{
do
	ElementInterface^ast := Signature^ast;
end
}

Signature -> vide identificateur parenthese_ouvrante #tds_Params Parametres parenthese_fermante #ast;
#tds_Params{
do
	Parametres^tds_Params := new SymbolTable();
end
}

#ast{
do
	if (Signature^tds.knows(identificateur^txt)) then
		error(Signature_ident_already_defined, identificateur^txt);
	else
		Signature^ast := Signature^factory.createSignature(identificateur^txt, Parametres^ast);
	end
end
}

Signature -> Type identificateur parenthese_ouvrante #tds_Params Parametres parenthese_fermante #ast;

#tds_Params{
do
	Parametres^tds_Params := new SymbolTable();
end
}

#ast{
do
	if (Signature^tds.knows(identificateur^txt)) then
		error(Signature_ident_already_defined, identificateur^txt);
	else
		Signature^ast := Signature^factory.createSignature(Type^ast, identificateur^txt, Parametres^ast);
	end
end
}


Classes -> #inh Classe #tree Classes #ast;
#inh {
do
    Classe^tdsMeth := new SymbolTable(Classes^tds);
    Classe^tdsAtt := new SymbolTable(Classes^tds);
end
}

#ast{
do
	Classes1^ast.addFirst(Classe^ast);
    Classes^ast := Classes1^ast;
end
}

#tree {
do
    Classes^tds.register(Classe^ast);
    Classes1^tds := Classes^tds;
end
}

Classes -> #ast;
#ast{
do
	Classes^ast := new LinkedList<Classe>();
end
}


Classe -> classe  identificateur_type HeritageClasse ImplantationInterface accolade_ouvrante #inh ElementsClasse accolade_fermante #ast;
#ast{
do
	if Classe^tds.knows(identificateur_type^txt) then
		error(Class_already_defined, identificateur_type^txt);
	else
		Classe^ast := Classe^factory.createClasse(identificateur_type^txt, HeritageClasse^ast, ImplantationInterface^ast, ElementsClasse^ast);
	end
end
}
#inh {
do
    ElementsClasse^nomClasse := identificateur_type^txt;
end
}

HeritageClasse -> extension identificateur_type #ast;
#ast{
local
    f : Optional<Declaration>;
    d : Declaration;
	l : LinkedList<Classe>;
do
	l := new LinkedList<Classe>();
	if HeritageClasse^tds.knows(identificateur_type^txt) then
        f := HeritageClasse^tds.get(identificateur_type^txt);
        d := f.get();
		match d
		with Classe then
            l.add(d);
		end
		HeritageClasse^ast := l;
	else
		error(CLASS_undefined_ident, identificateur_type^txt);
	end
end
}

HeritageClasse -> #ast;
#ast{
do
	HeritageClasse^ast := new LinkedList<Classe>();
end
}

ImplantationInterface -> implantation identificateur_type #tds SuiteHeritageInterface #ast;
#tds{
local
   l : List<String>;
do
	if (ImplantationInterface^tds.knows(identificateur_type^txt)) then
		l := new LinkedList<String>();
		l.add(identificateur_type^txt);
		SuiteHeritageInterface^listHeritage := l;
	else
		error(Interface_undefined_ident, identificateur_type^txt);
	end	
end
}


#ast{
local
   f : Optional<Declaration>;
   d : Declaration;
do
	f := ImplantationInterface^tds.get(identificateur_type^txt);
	d := f.get();
	match d
      		with Interface then SuiteHeritageInterface^ast.addFirst(d);
      	end
	ImplantationInterface^ast := SuiteHeritageInterface^ast;

end
}



ImplantationInterface -> #ast;
#ast{
do
	ImplantationInterface^ast := new LinkedList<Interface>();
end
}






Principale -> public classe identificateur_type accolade_ouvrante MethodePrincipale accolade_fermante #ast;
#ast{
local
    m : MethodePrincipale;
do
	m := MethodePrincipale^ast;
	m.setIdentificateurType(identificateur_type^txt);
	Principale^ast := m;
end
}


MethodePrincipale -> public statique vide principale parenthese_ouvrante type_String crochet_ouvrant crochet_fermant identificateur parenthese_fermante Bloc #ast;
#ast{
do
	MethodePrincipale^ast := new MethodePrincipaleImpl(Bloc^ast);
end
}





ElementsClasse -> DroitAcces #droit ElementClasse ElementsClasse #ast;
#ast {
do
    ElementsClasse1^ast.addFirst(ElementClasse^ast);
    ElementsClasse^ast := ElementsClasse1^ast;
end
}
#droit {
do
    ElementClasse^droit := DroitAcces^ast;
end
}
  
ElementsClasse -> #ast;
#ast{
do
	ElementsClasse^ast := new LinkedList<ElementClasse>();
end
}
 

DroitAcces -> public #ast;
#ast {
do
    DroitAcces^ast := DroitAcces^factory.createDroitAcces(0);
end
}
 
DroitAcces -> protege #ast;
#ast {
do
    DroitAcces^ast := DroitAcces^factory.createDroitAcces(1);
end
}
 
DroitAcces -> prive #ast;
#ast {
do
    DroitAcces^ast := DroitAcces^factory.createDroitAcces(2);
end
}  
 

ElementClasse -> statique AttributOuMethode #ast;
#ast {
do
    ElementClasse^ast := AttributOuMethode^ast;
    ElementClasse^ast.setStatic(true);
end
}  
 
ElementClasse -> AttributOuMethode #ast;
#ast {
do
    ElementClasse^ast := AttributOuMethode^ast;
end
}

ElementClasse -> Constructeur #ast;
#ast {
do
    ElementClasse^ast := Constructeur^ast;
	ElementClasse^tdsMeth.register(Constructeur^ast);
end
}
   

AttributOuMethode -> Type identificateur #inh SuiteAttributOuMethode #ast;
#ast {
do
    AttributOuMethode^ast := SuiteAttributOuMethode^ast;
end
}
#inh {
do
    SuiteAttributOuMethode^nomAtt := identificateur^txt;
    SuiteAttributOuMethode^typeAtt := Type^ast;
end
}


AttributOuMethode -> #factory vide identificateur parenthese_ouvrante Parametres parenthese_fermante Bloc #ast;
#factory{
do
	Parametres^tds_Params := new SymbolTable();
end
}
#ast {
local
    m : ElementClasse;
do
    m := AttributOuMethode^factory.createMethode(AttributOuMethode^droit, identificateur^txt, Parametres^ast, Bloc^ast);
    AttributOuMethode^ast := m;
    AttributOuMethode^tdsMeth.register(m);
end
}



SuiteAttributOuMethode -> point_virgule #ast;
#ast {
local
    a : ElementClasse;
do
        if SuiteAttributOuMethode^tdsAtt.contains(SuiteAttributOuMethode^nomAtt) then
        error(PROGRAMME_attribut_already_defined, SuiteAttributOuMethode^nomAtt);
    else
        a := SuiteAttributOuMethode^factory.createAttribut(SuiteAttributOuMethode^droit, SuiteAttributOuMethode^typeAtt, SuiteAttributOuMethode^nomAtt);
        SuiteAttributOuMethode^ast := a;
        SuiteAttributOuMethode^tdsAtt.register(a);
    end

end
}


SuiteAttributOuMethode -> #factory parenthese_ouvrante Parametres parenthese_fermante Bloc #ast;
#factory{
do
	Parametres^tds_Params := new SymbolTable();
end
}

#ast {
local
    m : ElementClasse;
do
    m := SuiteAttributOuMethode^factory.createMethode(SuiteAttributOuMethode^droit, SuiteAttributOuMethode^typeAtt, SuiteAttributOuMethode^nomAtt, Parametres^ast, Bloc^ast);
    SuiteAttributOuMethode^ast := m;
    SuiteAttributOuMethode^tdsMeth.register(m);
end
}

Constructeur -> #factory identificateur_type parenthese_ouvrante Parametres parenthese_fermante Bloc #ast;
#factory{
do
	Parametres^tds_Params := new SymbolTable();
end
}
 
#ast {
do
    if Constructeur^nomClasse.equals(identificateur_type^txt) then
        Constructeur^ast := Constructeur^factory.createConstructeur(identificateur_type^txt, Parametres^ast, Bloc^ast);
    else
        error(nom_constructeur_incorrect, identificateur_type^txt);
    end
end
}


Parametres -> #ast;
#ast{
do
	Parametres^ast := new LinkedList<Parametre>(); 
end
}

Parametres -> Parametre #tds SuiteParametres  #ast;
#ast {
do
    SuiteParametres^ast.addFirst(Parametre^ast);
    Parametres^ast := SuiteParametres^ast;
end
}
#tds {
do
    Parametres^tds_Params.register(Parametre^ast);
end
}


SuiteParametres -> #ast;
#ast {
do
    SuiteParametres^ast := new LinkedList<Parametre>();
end
}

SuiteParametres -> virgule Parametre #tds SuiteParametres #ast ;
#tds{
do
	SuiteParametres^tds_Params.register(Parametre^ast);
end
}

#ast {
do
    SuiteParametres1^ast.addFirst(Parametre^ast);
    SuiteParametres^ast := SuiteParametres1^ast;
end
}

Parametre -> Type identificateur #ast;
#ast{
do
	if Parametre^tds_Params.contains(identificateur^txt) then
		error(PARAM_already_defined, identificateur^txt);
	else
		Parametre^ast := Parametre^factory.createParametre(Type^ast, identificateur^txt);
	end
end
}

Type -> TypeAtomique #inh SuiteTypeAtomique #ast;
#inh{
do
	SuiteTypeAtomique^type := TypeAtomique^ast;
end
}

#ast{
do
	Type^ast := SuiteTypeAtomique^ast;
end
}


TypeAtomique -> type_bool #ast ;
#ast {
do
   TypeAtomique^ast := TypeAtomique^factory.createBooleanType();  
end
}

TypeAtomique -> type_int  #ast ;
#ast {
do
   TypeAtomique^ast := TypeAtomique^factory.createIntegerType(); 
end
}

TypeAtomique -> type_char #ast ;
#ast {
do
   TypeAtomique^ast := TypeAtomique^factory.createCharType();  
end
}

TypeAtomique -> type_String #ast ;
#ast {
do
   TypeAtomique^ast := TypeAtomique^factory.createStringType();  
end
}


--TypeAtomique -> identificateur_type ;


Bloc -> accolade_ouvrante #inh Instructions accolade_fermante #ast;
#ast {
do
    Bloc^ast := Bloc^factory.createBlock(Instructions^insts);
end
}
#inh {
do
    Instructions^tree := new ArrayList<Instruction>();
    Instructions^tds := new SymbolTable(Bloc^tds);
end
}

Instructions -> #ast;
#ast {
do
    Instructions^insts := Instructions^tree;
end
}

Instructions -> #tds Instruction #tree Instructions #ast;
#ast {
do
    Instructions^insts := Instructions1^insts;
end
}
#tree {
do
    Instructions^tree.add(Instruction^ast);
    Instructions1^tree := Instructions^tree;
end
}
#tds {
do
    Instruction^tds := Instructions^tds;
end
}

Instruction -> Type identificateur affectation #tds Expression point_virgule #ast #tds1;
#ast {
do
    Instruction^ast := Instruction^factory.createVariableDeclaration(identificateur^txt, Type^ast, Expression^ast);
end
}
#tds {
do
    Expression^tds := Instruction^tds;
end
}
#tds1 {
local
    d : Declaration;
do
    if Instruction^tds.knows(identificateur^txt) then
        error(Variable_unexpected,identificateur^txt);
    else
        d := Instruction^factory.createVariableDeclaration(identificateur^txt, Type^ast, Expression^ast);
        Instruction^tds.register(d);
    end
end
}





--Instruction ->  Expression point_virgule ; -- WARRRRNING


Instruction -> si parenthese_ouvrante #tds1 Expression parenthese_fermante #tds2 Bloc #tds3 SuiteConditionnelle #ast;
#ast {
do
    if (SuiteConditionnelle^ast = nil) then
        Instruction^ast := Instruction^factory.createConditional(Expression^ast, Bloc^ast);
    else
        Instruction^ast := Instruction^factory.createConditional(Expression^ast, Bloc^ast, SuiteConditionnelle^ast);
    end
end
}
#tds1 {
do
    Expression^tds := Instruction^tds;
end
}

#tds2 {
do
    Bloc^tds := Instruction^tds;
end
}
#tds3 {
do
    SuiteConditionnelle^tds := Instruction^tds;
end
}


SuiteConditionnelle -> #ast;
#ast {
do
    SuiteConditionnelle^ast := nil;
end
}

SuiteConditionnelle ->sinon #tds Bloc #ast;
#ast {
do
    SuiteConditionnelle^ast := Bloc^ast;
end
}
#tds {
do
    Bloc^tds := SuiteConditionnelle^tds;
end
}




Instruction -> tant_que parenthese_ouvrante #tds1 Expression parenthese_fermante #tds2 Bloc #ast;
#ast {
do
    Instruction^ast := Instruction^factory.createRepetition(Expression^ast, Bloc^ast);
end
}
#tds1 {
do
    Expression^tds := Instruction^tds;
end
}
#tds2 {
do
    Bloc^tds := Instruction^tds;
end
}




Instruction -> afficher Expression point_virgule #ast;
#ast {
do
    Instruction^ast := Instruction^factory.createPrinter(Expression^ast);
end
}



Instruction -> retour Expression point_virgule #ast;
#ast{
do
	Instruction^ast := Instruction^factory.createReturnInstruction(Expression^ast);
end
}


Expressions -> Expression SuiteExpressions #ast ;
#ast{
do
	SuiteExpressions^ast.addExpression(Expression^ast);
	Expressions^ast := SuiteExpressions^ast;
end
}

SuiteExpressions -> virgule Expression SuiteExpressions  #ast;
#ast{
do
	SuiteExpressions1^ast.addExpression(Expression^ast);
	SuiteExpressions^ast := SuiteExpressions1^ast;
end
}

SuiteExpressions ->  #ast;
#ast{
do
	SuiteExpressions^ast := SuiteExpressions^factory.createExpressions();
end
}


Expression -> Affectation #gauche SuiteAffectation #ast;
#gauche{
do
	SuiteAffectation^gauche := Affectation^ast;
end
}

#ast{
do
	Expression^ast := SuiteAffectation^ast;
end
}


Affectation -> Condition #gauche SuiteCondition #ast;
#gauche{
do
	SuiteCondition^gauche := Condition^ast;
end
}

#ast{
do
	Affectation^ast := SuiteCondition^ast;
end
}

SuiteAffectation -> affectation Expression #ast;
#ast{
do
	SuiteAffectation^ast := SuiteAffectation^factory.createAssignment(SuiteAffectation^gauche, Expression^ast);
end
}

SuiteAffectation -> #ast;
#ast{
do
	SuiteAffectation^ast := SuiteAffectation^gauche;
end
}


SuiteCondition -> Comparatif  Condition  #ast;
#ast{
do
	SuiteCondition^ast := SuiteCondition^factory.createBinaryExpression( SuiteCondition^gauche, Comparatif^bin_op, Condition^ast);
end
}

SuiteCondition ->  #ast;
#ast{
do
	SuiteCondition^ast := SuiteCondition^gauche;
end
}


Condition -> Terme  #gauche SuiteTerme  #ast;
#gauche{
do
	SuiteTerme^gauche := Terme^ast;
end
}

#ast{
do
	Condition^ast := SuiteTerme^ast;
end
}


SuiteTerme -> Additif Terme #gauche SuiteTerme  #ast;  
#gauche {
do
   SuiteTerme1^gauche := SuiteTerme^factory.createBinaryExpression(SuiteTerme^gauche,  Additif^bin_op,  Terme^ast);  
end 
}

#ast {
do 
   SuiteTerme^ast := SuiteTerme1^ast; 
end 
}

SuiteTerme -> #ast ;
#ast {
do
   SuiteTerme^ast := SuiteTerme^gauche;
end
}


Terme -> Facteur  #gauche SuiteFacteur #ast ;
#gauche{
do
	SuiteFacteur^gauche := Facteur^ast;
end
}

#ast{
do
	Terme^ast := SuiteFacteur^ast;
end
}

SuiteFacteur -> Multiplicatif Facteur #gauche  SuiteFacteur #ast ;  
#gauche {
do
	SuiteFacteur1^gauche := SuiteFacteur^factory.createBinaryExpression(SuiteFacteur^gauche,  Multiplicatif^bin_op,  Facteur^ast);  
end 
}

#ast{
do
	SuiteFacteur^ast := SuiteFacteur1^ast;
end
}

SuiteFacteur -> #ast;  
#ast{
do
	SuiteFacteur^ast := SuiteFacteur^gauche;
end
}


Facteur -> accolade_ouvrante Expressions accolade_fermante  #ast;
#ast{
do
	Facteur^ast := Expressions^ast;
end
}
Facteur -> soustraction Facteur  #ast;
#ast{
do
	 Facteur^ast := Facteur ^factory.createUnaryExpression(UnaryOperator.Opposite,Facteur1^ast);  
end
}  

Facteur -> negation Facteur  #ast;
#ast{
do
	Facteur^ast := Facteur ^factory. createUnaryExpression(UnaryOperator.Negate,Facteur1^ast);
end
}   

Facteur -> Valeur #ast;
#ast{
do
	Facteur^ast := Valeur^ast;
end
}  

Facteur -> nouveau Type  #type SuiteNouveau  #ast;
#type{
do
	SuiteNouveau^type := Type^ast;
end
}

#ast{
do
	Facteur^ast := SuiteNouveau^ast;
end
}

Facteur -> identificateur #inh AppelOuAcces #ast;
#inh{
do
	if Facteur^tds.knows(identificateur^txt) then
		AppelOuAcces^ident := identificateur^txt;
	else
		error(PROGRAMME_undefined_ident,identificateur^txt);
	end
end
}

#ast{
do
	Facteur^ast := AppelOuAcces^ast;
end
}


--Facteur -> identificateur_type point identificateur AppelOuAcces #ast;
--#ast{
--do
--	Facteur^ast := Facteur^factory.createStaticUse(identificateur_type^txt, identificateur^txt, AppelOuAcces^ast);
--end
--}


Facteur -> parenthese_ouvrante Expression parenthese_fermante #ast;
#ast{
do
	Facteur^ast := Expression^ast;
end
}




SuiteNouveau -> crochet_ouvrant Expression crochet_fermant #ast;
#ast{
do
	SuiteNouveau^ast := SuiteNouveau^factory.createArrayAllocation(SuiteNouveau^type,Expression^ast);
end
}

SuiteNouveau -> Arguments #ast;
#ast{
do
	SuiteNouveau^ast := SuiteNouveau^factory.createObjet(SuiteNouveau^type, Arguments^ast);
end
}


Arguments -> parenthese_ouvrante parenthese_fermante #ast;
#ast{
do
	Arguments^ast := Arguments^factory.createArguments();
end
} 

Arguments -> parenthese_ouvrante Expressions parenthese_fermante  #ast;
#ast{
do
	Arguments^ast := Arguments^factory.createArguments(Expressions^ast);
end
}


AppelOuAcces -> Acces #ast;
#ast{
do
	AppelOuAcces^ast := Acces^ast;
end
}

--AppelOuAcces -> Appel #ast;
--#ast{
--do
--	AppelOuAcces^ast := Appel^ast;
--end
--}


Acces ->  #ast;
#ast{
do
	Acces^ast := Acces^factory.createVariableUse(Acces^ident);
end
}

--Acces -> crochet_ouvrant Expression crochet_fermant  #inh Acces #ast;
--#inh{
--do
--	Acces1^support := Acces^factory.createArrayAccess(Access^support, Expression^ast);
--end
--}

--#ast{
--do
--	Acces^ast := Acces1^ast;
--end
--}

--Acces -> point identificateur #ident AppelOuAcces #ast;

--Appel -> Arguments Acces ;  





-- DEJA FAIT


TypeAtomique -> identificateur_type #ast;
#ast{
do
	TypeAtomique^ast := TypeAtomique^factory.createObjetType(identificateur_type^txt);
end
}

SuiteTypeAtomique -> crochet_ouvrant crochet_fermant #inh SuiteTypeAtomique #ast;
#inh {
do
    SuiteTypeAtomique1^type := SuiteTypeAtomique^factory.createArrayType(SuiteTypeAtomique^type);
end
}
#ast {
do
    SuiteTypeAtomique^ast := SuiteTypeAtomique1^ast;
end
}

SuiteTypeAtomique -> #ast;
#ast {
do
    SuiteTypeAtomique^ast := SuiteTypeAtomique^type;
end
}

Comparatif -> egalite #texte;
#texte {
do
   Comparatif^bin_op := BinaryOperator.Equals;
end
}



Comparatif -> different #texte;  
#texte {  
do  
   Comparatif^bin_op := BinaryOperator.Different;   
end  
}
  
Comparatif -> inferieur #texte;  
#texte {  
do  
   Comparatif^bin_op := BinaryOperator.Lesser;   
end  
}
  
Comparatif -> inferieur_egal #texte;  
#texte {  
do  
   Comparatif^bin_op := BinaryOperator.LesserOrEqual;   
end  
}
  
Comparatif -> superieur #texte;  
#texte {   
do  
   Comparatif^bin_op := BinaryOperator.Greater;    
end  
}
   
Comparatif -> superieur_egal #texte;  
#texte {   
do  
   Comparatif^bin_op := BinaryOperator.GreaterOrEqual;    
end  
}

 

Additif -> addition #ast;  
#ast { 
do  
   Additif^bin_op := BinaryOperator.Add;  
end  
}

Additif -> soustraction #ast;  
#ast {  
do  
   Additif^bin_op := BinaryOperator.Substract;   
end  
}
   
Additif -> ou #ast;  
#ast {  
do  
   Additif^bin_op := BinaryOperator.Or;   
end  
}  



Multiplicatif -> multiplication #ast; 
#ast {
do 
   Multiplicatif^bin_op := BinaryOperator.Multiply; 
end 
}

Multiplicatif -> division #ast; 
#ast { 
do 
   Multiplicatif^bin_op := BinaryOperator.Divide;  
end 
}
 
Multiplicatif -> modulo #ast; 
#ast {  
do 
   Multiplicatif^bin_op := BinaryOperator.Modulo;   
end 
}
  
Multiplicatif -> et #ast; 
#ast {  
do 
   Multiplicatif^bin_op := BinaryOperator.And;   
end 
} 


Valeur -> entier #ast;
#ast {
do
    Valeur^ast := Valeur^factory.createIntegerValue(entier^txt); 
end
}

Valeur -> vrai #ast;
#ast {
do
    Valeur^ast := Valeur^factory.createBooleanValue(true);  
end
}

Valeur -> faux #ast;
#ast {
do
    Valeur^ast := Valeur^factory.createBooleanValue(false);  
end
}

Valeur -> chaine #ast;
#ast {
do
    Valeur^ast := Valeur^factory.createStringValue(chaine^txt);
end
}

Valeur -> caractere #ast;
#ast {
do
    Valeur^ast := Valeur^factory.createCharValue(caractere^txt);
end
}

 
end
